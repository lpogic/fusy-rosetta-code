@class Sandpile
  -int{}{} grid
  
  @new Sandpile(int size)
    grid = new int{size}{size}
  <

  @void spawn(int x, int y, int size)
    grid{x}{y} = size
  <

  @boolean topple()
    #spreaded = false
    #xMax = grid.length - 1
    #yMax = grid{0}.length - 1
    for #x range(0, xMax), for #y range(0, yMax)
      if grid{x}{y} > 3
        grid{x}{y} -= 4
        if x > 0, grid{x - 1}{y} += 1
        if x < xMax, grid{x + 1}{y} += 1
        if y > 0, grid{x}{y - 1} += 1
        if y < yMax, grid{x}{y + 1} += 1
        spreaded = true
      <
    <
    return spreaded
  <

  @void draw()
    #xMax = grid.length - 1
    #yMax = grid{0}.length - 1
    for #y range(0, yMax)
      for #x range(0, xMax)
        if grid{x}{y} > 2, print("\# ")
        elf grid{x}{y} > 1, print("+ ")
        elf grid{x}{y} > 0, print("- ")
        elf, print("  ")
      <
      pln()
    <
  <
<

#sandpile = new Sandpile(20)
sandpile.spawn(10,10,512)
while sandpile.topple() <
sandpile.draw()
