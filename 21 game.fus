@interface AI
  @?int pick(int currentTotal)
<

@class SAI implements AI
  @int pick(int currentTotal)
    if currentTotal > 17, return 21 - currentTotal
    if currentTotal < 1, return 1
    return random:[ 1 [] 2 [] 3 ]:int
  <
<

#runningTotal = 0
#ai = new SAI()
#cmd = ""
if random(0.5)
  #aiPick = ai.pick(runningTotal)
  io:println("Computer picks {aiPick}")
  runningTotal += aiPick
  io:println("Running total: {runningTotal}")
<
while !"exit".equals(cmd)
  cmd = io:readln("21 game> ")
  switch cmd
    case "help" | "?", io:println("
      help, ? - shows help\n
      total - prints running total\n
      1 - adds 1 to running total\n
      2 - adds 2 to running total\n
      3 - adds 3 to running total\n
      exit - close this program")
    case "total", io:println("Running total: {runningTotal}")
    case "exit" <
    rest switch cmd
      case "1" | "2" | "3"
        runningTotal += Integer.parseInt(cmd)
        io:println("Running total: {runningTotal}")
        if runningTotal == 21, io:println("YOU WIN!") \ return
        if runningTotal > 21, io:println("YOU LOOSE!") \ return
        #aiPick = ai.pick(runningTotal)
        io:println("Computer picks {aiPick}")
        runningTotal += aiPick
        io:println("Running total: {runningTotal}")
        if runningTotal == 21, io:println("YOU LOOSE!") \ return
        if runningTotal > 21, io:println("YOU WIN!") \ return
      <
      rest io:println("Invalid cmd '{cmd}'. Type 'help' to list commands")
    <
  <
<